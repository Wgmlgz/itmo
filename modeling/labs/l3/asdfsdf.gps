**********************************************************************************************
*                              Модель СМО G/G/K/E                                            *
**********************************************************************************************
*                           И с х о д н ы е   д а н н ы е                                    *
**********************************************************************************************
p_not_1	EQU	0.4; Вероятность перехода в 3 из 2 состояния 
E_buf_1	EQU	1; емкость накопителя (буфера) 1 прибора                                         
E_buf_2	EQU	0; емкость накопителя (буфера) 2 прибора   
t_a	EQU	1; средний интервал между поступающими заявками                        
t_a_min	EQU	1; минимальный интервал между заявками (для равномерного распределения) 
t_a_max	EQU	3; максимальный интервал между заявками (для равномерного распределения)
t_b	EQU	2; средняя длительность обслуживания заявки в приборе                     
RN_a	EQU	20; номер генератора для потока                                          
RN_b	EQU	553; номер генератора для длительности обслуживания                      
* Параметры гиперэкспоненциального распределения:                                            
RN_H	EQU	91; номер генератора для гиперэкспоненциального распределения             
qq	EQU	0.1; вероятность выбора первой фазы                                      
tt_1	EQU	9.8; мат. ожидание первой фазы гиперэкспоненциального распределения      
tt_2	EQU	0.47; мат. ожидание второй фазы гиперэкспоненциального распределения     
* Параметры гипоэкспоненциального распределения (Эрланга):                                 
k_erl	EQU	2; порядок распределения Эрланга                                      
RN_erl1	EQU	31; номер первого генератора для распределения Эрланга 2-го порядка
RN_erl2	EQU	125; номер второго генератора для распределения Эрланга 2-го порядка 
******************************************************************************************** 
TU_unit_1	TABLE	M1,0.2,0.2,50;
TU_unit_2	TABLE	M1,0.2,0.2,50;
TU_unit_3	TABLE	M1,0.2,0.2,50;
unit_1	STORAGE	1
unit_2	STORAGE	1
unit_3	STORAGE	1
Erl_2	VARIABLE	(Exponential(RN_erl1,0,t_a/2))+(Exponential(RN_erl2,0,t_a/2)); сл.величина по закону Эрланга 2-го порядка
****************************************************************************
* Служебные переменные, необходимые для процедуры GetRandomNumberFromFile  *
****************************************************************************
ErrorCodes   MATRIX ,2,1  ; Коды ошибок открытия/закрытиия файла (при наличии ошибок в конце моделирования будут записаны ненулевые значения)  
FilePosition MATRIX ,1,1  ; Текущий номер строки в файле, из которой читается число (увеличивается на 1 с каждым чтением)
*********************************************************************
* В качестве исполняемого оставить только ОДИН оператор GENERATE !!!*
*********************************************************************
GENERATE	(Exponential(RN_a,0,t_a))
*GENERATE	(GetRandomNumberFromFile("numbers.txt"))
*GENERATE	(hyper1(RN_H, qq, tt_1, tt_2))
*GENERATE	V$Erl_2

mux_1	TRANSFER	p_not_1,,pribor_2	; Распределение на 2 или 3 прибор
pribor_1	TEST	L	Q$buf_2,E_buf_1,reject_1	; Проверка возможности войти в очередь/прибор 1
	QUEUE	buf_2
	SEIZE	unit_2
	DEPART	buf_2
	ADVANCE	(Exponential(Rn_b,0,t_b))
	RELEASE	unit_2
	TERMINATE	1
	TABULATE 	TU_unit_2

*pribor_2	TEST	L	Q$buf_3,E_buf_2,reject_2	; Проверка возможности войти в очередь/прибор 1
*	QUEUE	buf_3
pribor_2	TRANSFER	BOTH,,reject_2
	SEIZE	unit_3
	ADVANCE	(Exponential(Rn_b,0,t_b))
	RELEASE	unit_3
	TERMINATE	1
	TABULATE 	TU_unit_3

reject_1	SAVEVALUE p_reject_1,((1 + N$reject_1)/N$pribor_1)
	TERMINATE 1
reject_2	SAVEVALUE p_reject_2,((1 + N$reject_2)/N$pribor_2)
	TERMINATE 1
**************************************************************
* Процедура возвращает следующее прочитанное из файла число. *
* Числа в файле расположены по одному на каждой строчке.     *
* При выходе за границы файла чтение начинается с начала.    *
**************************************************************
PROCEDURE GetRandomNumberFromFile(FileName) BEGIN
    TEMPORARY OpenError, CloseError, LineFromFile, FileId;
    FileId = 1;
    OpenError = open(FileId,FileName);
    if (OpenError /= 0) then begin
        FileId = 2;
        OpenError = open(FileId,FileName);
        if (OpenError /=0) then begin
            ErrorCodes[1,1] = OpenError;
            return "";
        end;
    end;
    FilePosition[1,1] = FilePosition[1,1] + 1;
    seek(FileId,FilePosition[1,1]);
    LineFromFile = read(FileId);
    if (LineFromFile = "") then begin
        FilePosition[1,1] = 1;
        seek(FileId,FilePosition[1,1]);
        LineFromFile = read(FileId);
    end;
    CloseError = close(FileId);
    if (CloseError /=0) then begin
        ErrorCodes[2,1] = CloseError;
        return "";
    end;
    return value(LineFromFile);
END;
************************************************************
* Процедура возвращает значение псевдослучайной величины,  *
* распределенной по гиперэкспоненциальному закону, в       *
* соответствии с параметрами распределения qq, tt_1, tt_2. *
************************************************************
PROCEDURE hyper1(RN_H, qq, tt_1, tt_2) BEGIN
	if (uniform(1,0,1) < qq) then return exponential(RN_H,0,tt_1);
	else return exponential(RN_H,0,tt_2);		
END;

